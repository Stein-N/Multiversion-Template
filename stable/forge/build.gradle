plugins {
    id 'loader'
    id 'net.minecraftforge.gradle'
    id 'net.minecraftforge.accesstransformers' version '5.0.1'
    id 'net.minecraftforge.jarjar' version '0.2.3'
}

repositories {
    maven minecraft.mavenizer
    maven fg.forgeMaven
    maven fg.minecraftLibsMaven
}

dependencies {
    implementation minecraft.dependency("net.minecraftforge:forge:${minecraft_version}-${forge}")
    annotationProcessor 'net.minecraftforge:eventbus-validator:7.0-beta.10'
}

minecraft {
    mappings channel: 'official', version: minecraft_version

    def at = file('../common/src/main/resources/META-INF/accesstransformer.cfg')
    if (at.exists()) accessTransformers = at

    runs {
        configureEach {
            workingDir.convention layout.projectDirectory.dir('../common/runs/client')

            systemProperty 'forge.logging.console.level', 'debug'
            systemProperty 'eventbus.api.strictRuntimeChecks', 'true'
            args "--mixin.config=${mod_id}.mixins.json"
        }

        register('client') {
            systemProperty 'forge.enabledGameTestNamespaces', mod_id
        }
    }
}

jar {
    manifest {
        attributes([
                'MixinConfigs': "${mod_id}.mixins.json"
        ])
    }
}


sourceSets.each {
    def dir = layout.buildDirectory.dir("sourcesSets/$it.name")
    it.output.resourcesDir = dir
    it.java.destinationDirectory = dir
}

publishMods {
    file = jar.archiveFile
    modLoaders.addAll("forge")
}